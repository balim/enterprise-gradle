// In this section you declare repositories and dependencies which are used by the build scripts
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.0'
    }
}

apply plugin: 'war'
apply plugin: 'idea'
apply plugin: 'com.bmuschko.tomcat'

// Apply custom plugins to the project
apply from: 'gradle/jacoco.gradle'
apply from: 'gradle/integTest.gradle'
apply from: 'gradle/sonar.gradle'


group = 'org.gradle.training'

sourceCompatibility = 1.7
targetCompatibility = 1.7

// In this section you declare where to find the dependencies of your project
repositories {
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()
}
// In this section you declare the dependencies for your production and test code
dependencies {
    def springVersion='4.1.1.RELEASE'
    compile 'ch.qos.logback:logback-classic:1.1.2'
    compile "org.springframework:spring-webmvc:${springVersion}"
    compile 'org.apache.httpcomponents:httpclient:4.3.6'
    compile 'commons-io:commons-io:2.4'
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
    providedCompile 'javax.servlet:servlet-api:2.5'
    runtime 'jstl:jstl:1.2'
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.10.8'
    testCompile "org.springframework:spring-test:${springVersion}"

    def tomcatVersion = '7.0.57'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
    tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}") {
        exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
    }
}

[tomcatRun, tomcatRunWar]*.contextPath = 'sample-app'